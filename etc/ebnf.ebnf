/* An EBNF grammar for EBNF */
[1] ebnf        ::= (declaration | rule)*

[2] declaration ::= '@terminals' | pass

[3] rule        ::= lhs '::=' expression

# Rule identifier matches ENUM, but content should come from Symbol
[4] lhs         ::= ENUM SYMBOL

[5] expression  ::= alt

[6] alt         ::= seq ('|' seq)*

[7] seq         ::= diff+

[8] diff        ::= postfix ('-' postfix)*

[9] postfix     ::= primary POSTFIX?

[10] primary    ::= HEX
                |   SYMBOL
                |   RANGE
                |   ENUM
                |   O_RANGE
                |   O_ENUM
                |   STRING1
                |   STRING2
                |   '(' expression ')'

[11] pass       ::= '@pass' PASS

@terminals

[12] SYMBOL     ::= ([a-z] | [A-Z] | [0-9] | "_" | ".")+

[13] HEX        ::= '#x' ([0-9] | [a-f] | [A-F])+

[14] RANGE      ::= '[' CHAR '-' CHAR ']'

[15] ENUM       ::= '[' CHAR+ ']'

[16] O_RANGE    ::= '[^' CHAR '-' CHAR ']'

[17] O_ENUM     ::= '[^' CHAR+ ']'

[18] STRING1    ::= '"' (CHAR | [\t'\[\]\(\)-])* '"'

[19] STRING2    ::= "'" (CHAR | [\t"\[\]\(\)-])* "'"

[20] CHAR       ::= HEX
                |   ('\\' [\\trn'"\[\]\(\)-])
                |   [^\t\r\n'"\[\]\(\)-]

# Should be able to do this inline, but not until terminal regular expressions are created automatically
[21] POSTFIX    ::= [?*+]

[22] PASS       ::= ( [#x20\t\r\n]
                    | ( '#' | '//' ) [^\r\n]*
                    | '/*' (( '*' [^/] )? | [^*] )* '*/'
                    )+

@pass           ::= PASS

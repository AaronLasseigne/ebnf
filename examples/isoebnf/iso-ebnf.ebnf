# W3C EBNF for ISO/IEC 14977 : 1996 EBNF
# (Scoured from https://www.cl.cam.ac.uk/~mgk25/iso-14977.pdf)

[1]  syntax            ::= syntax_rule*

[2]  syntax_rule       ::= meta_identifier defining_symbol definitions_list terminator_symbol

[3]  definitions_list  ::= single_definition (definition_separator_symbol definitions_list)*

[4]  single_definition ::= term (',' term)*

[5]  term              ::= factor ('-' exception)?

[6]  exception         ::= factor

[7]  factor            ::= (integer '*')? primary

[8]  primary           ::= optional_sequence
                         | repeated_sequence
                         | special_sequence
                         | grouped_sequence
                         | meta_identifier
                         | terminal_string
                         | empty

[9]  optional_sequence ::= start_option_symbol definitions_list end_option_symbol

[10] repeated_sequence ::= start_repeat_symbol definitions_list end_repeat_symbol

[11] grouped_sequence  ::= '(' definitions_list ')'

# Note, the following are nominally terminal rules,
# although ISO EBNF does not really distinguish between non-terminal and terminal rules.

@terminals

[12] letter            ::= [a-zA-Z]
[13] decimal_digit     ::= [0-9]

[14] integer           ::= decimal_digit+

[15] meta_identifier             ::= letter meta_identifier_character*

# Extended to allow '_'
[16] meta_identifier_character   ::= letter | decimal_digit | '_'
                            
[17] terminal_string             ::= ("'" first_terminal_character+ "'")
                                   | ('"' second_terminal_character+ '"')

[18] first_terminal_character    ::= terminal_character - "'"

[19] second_terminal_character   ::= terminal_character - '"'

[20] special_sequence            ::= '?' special_sequence_character* '?'

[21] special_sequence_character  ::= terminal_character - '?'

[22] terminal_character          ::= letter
                                   | decimal_digit
                                   | concatenate_symbol
                                   | defining_symbol
                                   | definition_separator_symbol
                                   | end_comment_symbol
                                   | end_group_symbol
                                   | end_option_symbol
                                   | end_repeat_symbol
                                   | except_symbol
                                   | first_quote_symbol
                                   | repetition_symbol
                                   | second_quote_symbol
                                   | special_sequence_symbol
                                   | start_comment_symbol
                                   | start_group_symbol
                                   | start_option_symbol
                                   | start_repeat_symbol
                                   | terminator_symbol
                                   | other_character

[23] other_character            ::= [:+_%@&$<>^` Ìƒ#x20#x23] | '\'

[24] gap_separator              ::= [#x9#xa#xb#xc#xd#x20]

@pass                           gap_separator+

[25] empty                      ::= ''

# Simple terminals that are often extended
[26] defining_symbol            ::= '=' | ':'
[27] definition_separator_symbol ::= '|' | '/' | '!'
[28] terminator_symbol           ::= ';' | '.'
[29] start_option_symbol         ::= '[' | '(/'
[30] end_option_symbol           ::= ']' | '/)'
[31] start_repeat_symbol         ::= '{' | '(:'
[32] end_repeat_symbol           ::= '}' | ':)'

# Symbols described, but not actually used.

[33] gap_free_symbol            ::= (terminal_character - ['"])
                                  | terminal_string

[34] repetition_symbol          ::= '*'
[35] except_symbol              ::= '-'
[36] concatenate_symbol         ::= ','
[37] first_quote_symbol         ::= "'"
[38] second_quote_symbol        ::= '"'
[39] start_comment_symbol       ::= '(*'
[40] end_comment_symbol         ::= '*)'
[41] start_group_symbol         ::= '('
[42] end_group_symbol           ::= ')'
[43] special_sequence_symbol    ::= '?'
